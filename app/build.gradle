apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "co.lateralview.myapp"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            buildConfigField "String", "BASE_URL", "\"https://base-url-for-debug\""
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "BASE_URL", "\"https://base-url-for-release\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext {
    version_support = '27.1.1'
    version_butterknife = "8.8.1"
    version_rxjava = '2.1.14'
    version_rxandroid = '2.0.2'
    version_dagger = "2.8"
    version_retrofit = "2.3.0"
    version_leakcanary = "1.5.4"
    version_permission = '3.0.1'
    version_firebase = '11.6.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:recyclerview-v7:$version_support"
    implementation "com.android.support:design:$version_support"
    implementation "com.android.support:support-v13:$version_support"
    implementation "com.android.support:appcompat-v7:$version_support"

    // Dagger 2 - Dependency Injection
    implementation "com.google.dagger:dagger:$version_dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$version_dagger"

    // Butterknife - View binding
    implementation "com.jakewharton:butterknife:$version_butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$version_butterknife"

    //Image Loader
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.2'

    //Instance State Handler
    implementation 'frankiesardo:icepick:3.2.0'
    compileOnly 'frankiesardo:icepick-processor:3.2.0'

    //Image Crop
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'

    //Reactive Programming
    implementation "io.reactivex.rxjava2:rxjava:$version_rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$version_rxandroid"

    // Leak Canary - memory leak detection
    // LeakCanary for debug builds only: notifications, analysis, etc
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$version_leakcanary"
    // No-Op version of LeakCanary for release builds: no notifications, no analysis, nothing
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$version_leakcanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$version_leakcanary"

    // REST Client
    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$version_retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$version_retrofit"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    // Stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    //Firebase
    implementation "com.google.firebase:firebase-core:$version_firebase"
    releaseImplementation "com.google.firebase:firebase-crash:$version_firebase"

    //PermissionsDispatcher
    implementation("com.github.hotchemi:permissionsdispatcher:$version_permission")
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$version_permission"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.17.0'
    testImplementation "org.robolectric:robolectric:3.8"

    // Friendly Http Inspector through separate App
    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'

    // Proguard Annotations
    implementation 'com.infstory:proguard-annotations:1.0.2'
}
